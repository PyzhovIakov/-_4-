Процедура ОбработкаПроведения(Отказ, Режим)

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВремТ
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	|	И РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремТ.Номенклатура,
	|	ВремТ.Количество,
	|	ВремТ.Сумма,
	|	ОстаткиНоменклатурыОстатки.Склад,
	|	ЕстьNull(ОстаткиНоменклатурыОстатки.КоличествоОстаток,0) как КоличествоОстаток,
	|	ЕстьNull(ОстаткиНоменклатурыОстатки.СуммаОстаток,0) как СуммаОстаток
	|ПОМЕСТИТЬ ВремТ1
	|ИЗ
	|	ВремТ КАК ВремТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(&Дата, Номенклатура В
	|			(ВЫБРАТЬ
	|				ВремТ.Номенклатура
	|			ИЗ
	|				ВремТ КАК ВремТ)) КАК ОстаткиНоменклатурыОстатки
	|		ПО ВремТ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремТ1.Номенклатура КАК Номенклатура,
	|	ВремТ1.Количество КАК Количество,
	|	ВремТ1.Сумма,
	|	ВремТ1.Склад,
	|	ВремТ1.КоличествоОстаток КАК КоличествоОстаток,
	|	ВремТ1.СуммаОстаток,
	|	ВЫБОР
	|		КОГДА &Склад = ВремТ1.Склад
	|			ТОГДА -1
	|		ИНАЧЕ ПриоритетСкладовСрезПоследних.Приоритет
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	ВремТ1 КАК ВремТ1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриоритетСкладов.СрезПоследних(&Дата, Склад В
	|			(ВЫБРАТЬ
	|				ВремТ1.Склад
	|			ИЗ
	|				ВремТ1 КАК ВремТ1)) КАК ПриоритетСкладовСрезПоследних
	|		ПО ВремТ1.Склад = ПриоритетСкладовСрезПоследних.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет
	|ИТОГИ
	|	МАКСИМУМ(Количество) КАК Количество,
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура";

	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ВидНоменклатуры", Перечисления.ВидыНоменклатуры.Товар);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Отказ=Истина;
			Сообщить("Товара " + ВыборкаНоменклатура.Номенклатура + " нехватает. кол-во есть = "
				+ ВыборкаНоменклатура.КоличествоОстаток);
		КонецЕсли;
		Выборка = ВыборкаНоменклатура.Выбрать();
		Осталось = ВыборкаНоменклатура.Количество;
		Пока Осталось > 0 И Выборка.Следующий() Цикл
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			Движение.Период = Дата;
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Склад = Выборка.Склад;
			Движение.Количество = Мин(Осталось, Выборка.КоличествоОстаток);
			Движение.Сумма =Движение.Количество / Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			Осталось=Осталось - Движение.Количество;
		КонецЦикла;
	КонецЦикла;
	Движения.ОстаткиНоменклатуры.Записывать=Истина;
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры